[TOC]

# 01.Reversing



将可执行程序反汇编，通过分析反汇编代码来理解其代码功能（例如各接口的数据结构等），然后用高级语言重新描述这段代码，逆向分析原始软件的思路，这个过程就称作逆向工程（Reverse Engineering），有时也简单地称作逆向（Reversing）。

逆向工程可以让我们了解程序的结构及程序的逻辑，因此，利用逆向工程可以洞察程序的运行过程。

## 1. 32位软件逆向技术

### 1.1 启动函数

对于win32应用程序的编写时，我们都需要实现一个`WinMain`函数，但在这个函数开始之前，首先被执行的是启动函数的相关代码。这段代码由编译器生成，用于初始化进程，完成后才会调用`WinMain`函数。对于Visual C++程序亦是如此。

所有c/c++程序运行时，启动函数的作用基本相同，包括检索指向新进程的命令行指针、检索指向新进程的环境变量指针、全局变量初始化和内存栈初始化等。当所有的初始化操作完成后，启动函数就会调用应用程序的进入点函数（`main`和`WinMain`）。调用`WinMain`函数的示例如下：

```c++
GetStartupInfo (&StartupInfo);
Int nMainRetVal = WinMain(GetModuleHandle(NULL),
                          NULL,
                          pszCommandLineAnsi,
                          (StartupInfo.dwFlags&STARTF_USESHOWWINDOW)?
                          StartupInfo.wShowWindow:SW_SHOWDEFFAULT);
```

进入点返回时，启动函数便调用C运行库的`exit`函数，将返回值`nMainRetVal`传递给它，进行一些必要的处理，最后调用系统函数`ExitProcess`退出。

开发人员可以修改启动源代码，但这样做会导致即使是同一编译器，生成的启动代码也不同。在分析程序的过程中，可以略过启动代码，直接将重点放到WinMain函数体上。

### 1.2 函数

程序都是由具有不同功能的函数组成的，因此在逆向分析中将重点放在函数的识别及参数的传递上是明智的。这样做可以将注意力集中在某一段代码上。函数是一个程序模块，用来实现一个特定的功能。一个函数包括函数名、入口参数、返回值、函数功能等部分。

#### 函数的识别

程序通过调用程序来调用函数，在程序执行后又返回调用程序继续执行。调用函数的代码中保存了一个返回地址，该地址会与参数一起传递给被调用的函数。有多种方法可以实现这个功能，在绝大多数的情况下，编译器都会使用`call`和`ret`指令来调用函数及返回调用位置。

`call`指令与跳转指令功能类似，但`call`指令保存返回信息，即将其之后的指令地址压入栈的顶部，当遇到`ret`指令时返回这个地址。`call`指令给出的地址就是被调用函数的起始地址。`ret`指令则用于结束函数的执行。通过这个机制可以很容易地把函数调用和其他跳转指令区别开来。

因此，可以通过定位`call`指令或者利用`ret`指令结束的标志来识别函数。`call`指令的操作数就是所调用函数的首地址。

```c
#include<stdio.h>

int Add(int x,int y);

main()
{
	int a=5,b=6;
	Add(a,b);
	return 0;
}

Add(int x,int y)
{
	return (x+y);
}

```



``` shell
 gcc -m32 -o main main.c
 objdump -d main -M intel,i386

00000000000005fa <main>:
 5fa:	55                   	push   ebp
 5fb:	48                   	dec    eax
 5fc:	89 e5                	mov    ebp,esp
 5fe:	48                   	dec    eax
 5ff:	83 ec 10             	sub    esp,0x10
 602:	c7 45 f8 05 00 00 00 	mov    DWORD PTR [ebp-0x8],0x5
 609:	c7 45 fc 06 00 00 00 	mov    DWORD PTR [ebp-0x4],0x6
 610:	8b 55 fc             	mov    edx,DWORD PTR [ebp-0x4]
 613:	8b 45 f8             	mov    eax,DWORD PTR [ebp-0x8]
 616:	89 d6                	mov    esi,edx
 618:	89 c7                	mov    edi,eax
 61a:	e8 07 00 00 00       	call   626 <Add>	//此处调用Add()函数，0x626为其函数首地址
 61f:	b8 00 00 00 00       	mov    eax,0x0
 624:	c9                   	leave  
 625:	c3                   	ret    

0000000000000626 <Add>:
 626:	55                   	push   ebp
 627:	48                   	dec    eax
 628:	89 e5                	mov    ebp,esp
 62a:	89 7d fc             	mov    DWORD PTR [ebp-0x4],edi
 62d:	89 75 f8             	mov    DWORD PTR [ebp-0x8],esi
 630:	8b 55 fc             	mov    edx,DWORD PTR [ebp-0x4]
 633:	8b 45 f8             	mov    eax,DWORD PTR [ebp-0x8]
 636:	01 d0                	add    eax,edx
 638:	5d                   	pop    ebp
 639:	c3                   	ret		//这是Add()函数的结尾，以ret作结
 63a:	66 0f 1f 44 00 00    	nop    WORD PTR [eax+eax*1+0x0]

```



#### 函数的参数

函数传递参数有三种方式：栈方式、寄存器方式及通过全局变量进行隐含参数传递方式。

1. 利用栈传递参数：

   栈是一种“后进后出”的存储区，栈顶指针`esp`指向栈中第1个可用的数据项。在调用函数时，调用者依次把参数压入栈中，然后调用函数。函数被调用后，在栈中取得数据并进行计算。计算完毕后，由调用者或者函数本身修改栈，使栈恢复原样（即平衡栈数据）。

   在程序设计语言中为了实现函数调用而建议的协议称为调用约定。这种协议规定了函数中参数的传送方式、参数是否可变和由谁来处理栈平衡等问题。不同的语言定义了不同的调用约定：

   | 约定类型       | __cdecl(c规范) | pascal   | stdcall  | Fastcall |
   | -------------- | -------------- | -------- | -------- | -------- |
   | 参数传递顺序   | 从右到左       | 从左到右 | 从右到左 | 从右到左 |
   | 平衡栈者       | 调用者         | 子程序   | 子程序   | 子程序   |
   | 允许使用VARARG | 是             | 否       | 是       |          |

   `__cdecl`是c和c++程序的默认调用约定。c/c++和MFC程序默认使用的调用约定就是`__cdecl`，也可以在函数声明时加上`__cdecl`关键字来手动指定。

   `pascal`规范按从左到右的顺序压参入栈，要求被调用函数负责栈平衡

   `stdcall`调用约定是Win32 API采用的约定方式，有“标准调用”之意，如果有使用`VARARG`参数类型，就是调用程序平衡栈，否则就是子程序平衡栈。

   ```asm
   __cdecl:
   push par3	;参数从右往左传递
   push par2
   push par1
   call test1	;平衡栈
   add esp,0xc
   
   __pascal:
   push par1	;参数从左往右传递
   push par2
   push par3
   call test1	;函数内平衡栈
   
   __stdcall:
   push par3	;参数从右往左传递
   push par2
   push par1
   call test1	;函数内平衡栈
   ```

   函数对参数的存取及局部变量都是通过栈来定义的，非优化编译器用一个专门的寄存器（通常是`ebp`）对参数进行寻址。c、c++、pascal等高级语言的函数（子程序）执行过程基本一致：

   - 调用者将函数（子程序）执行完毕时应返回的地址、参数压栈
   - 子程序使用`ebp+偏移量`对栈中的参数进行寻址并取出，完成操作
   - 子程序使用`ret`或者`ref`指令返回。此时CPU将`eip`置为栈中保存的地址，并继续执行。

   在许多时候，编译器会按优化方式来编译程序，栈寻址稍有不同。这时编译器为了节省`ebp`寄存器或尽可能减少代码以提高速度，会直接通过`esp`对参数进行寻址。

2. 利用寄存器传递参数：

   寄存器传递参数的方式没有标准，所有与平台相关的方式都是由编译器开发人员制定的。尽管没有标准，但绝大多数编译器提供商都在不对兼容性进行声明的情况下遵循相应的规范，即`Fastcall`规范。

   另一类调用规范`thiscall`也用到了寄存器传递参数。`thiscall`是c++中非静态类成员函数的默认调用约定，对象的每个函数隐含接受this参数。采用`thiscall`约定时，函数的参数按照从右到左的顺序入栈，被调用的函数在返回前清理传送参数的栈，仅通过`ecx`传送一个额外的参数——this指针。

3. 名称修饰约定：

   为了允许使用操作符和函数重载，c++编译器往往会按照某种规则改写每一个入口点的符号名，从而允许同一个名字（具有不同的参数类型或者不同作用域）有多个用法且不会破坏现有的基于C的连接器。这项技术通常成为名称改编或者名称修饰。

   c编译时函数名修饰约定规则如下：

   - `stdcall`在输出函数名前面加一个下划线前缀，后面加一个“@”及其参数的字节数，格式为“_functionname@number”
   - `__cdecl`仅在输出函数名前面加一个下划线前缀，格式为“_functionname”
   - `Fastcall`在输出函数名前面加一个“@”，在后面加一个“@”及其参数的字节数，格式为“@functionname@number”

   它们均不改变输出函数名中的字符大小写。这个`pascal`调用约定不同。`pascal`约定输出的函数名不能有任何修饰且全部为大写。

   c++编译时函数名修饰约定规则如下：

   - `stdcall`以“？”标识函数名的开始，后跟函数名；在函数吗后面，以“@@YG”表示参数表的开始，后跟参数表；参数表的第1项为该函数的返回值类型，其后依次为参数的数据类型，指针标识在其所指数据类型前；在参数表后面，以“@Z”标识整个名字的结束（如果该函数没有参数，则以“Z”标识结束）。其格式为“？functionname@@YG\*\*\*\*\*@Z”或“？functionname@@YG\*XZ”
   - `__cdecl`调用约定规则与上面`stdcall`调用约定规则一致，只是参数表的开始标识由“@@YG”变成了“@@YA”
   - `Fastcall`调用约定规则与上面`stdcall`调用约定规则一致，只是参数表的开始标识由“@@YG”变成了“@@YI”



#### 函数的返回值

函数被调用执行后，将向调用者返回1个或者多个执行结果，称为函数返回值。返回值最常见的形式是return操作符，还有通过按传参引用方式返回值、通过全局变量返回值等。

1. 用return操作符返回值：

   ```c
   #include<stdio.h>
   
   int myAdd(int x,int y);
   
   int main()
   {
   	int a=5,b=6;
   	myAdd(a,b);
   
   	return 0;
   }
   
   int myAdd(int x,int y)
   {
   	int temp;
   	temp = x+y;
   	return temp;
   }
   ```

   ```shell
   gcc -m32 -o main mian.c
   objdump -d main -M intel
   
   000004ed <main>:
    4ed:	8d 4c 24 04          	lea    ecx,[esp+0x4]
    4f1:	83 e4 f0             	and    esp,0xfffffff0
    4f4:	ff 71 fc             	push   DWORD PTR [ecx-0x4]
    4f7:	55                   	push   ebp
    4f8:	89 e5                	mov    ebp,esp
    4fa:	51                   	push   ecx
    4fb:	83 ec 14             	sub    esp,0x14
    4fe:	e8 51 00 00 00       	call   554 <__x86.get_pc_thunk.ax>
    503:	05 d9 1a 00 00       	add    eax,0x1ad9
    508:	c7 45 f0 05 00 00 00 	mov    DWORD PTR [ebp-0x10],0x5	；变量a
    50f:	c7 45 f4 06 00 00 00 	mov    DWORD PTR [ebp-0xc],0x6	；变量b
    516:	83 ec 08             	sub    esp,0x8
    519:	ff 75 f4             	push   DWORD PTR [ebp-0xc]	
    51c:	ff 75 f0             	push   DWORD PTR [ebp-0x10]	；从右往左传参
    51f:	e8 10 00 00 00       	call   534 <myAdd>
    524:	83 c4 10             	add    esp,0x10
    527:	b8 00 00 00 00       	mov    eax,0x0
    52c:	8b 4d fc             	mov    ecx,DWORD PTR [ebp-0x4]
    52f:	c9                   	leave  
    530:	8d 61 fc             	lea    esp,[ecx-0x4]
    533:	c3                   	ret    
   
   00000534 <myAdd>:
    534:	55                   	push   ebp
    535:	89 e5                	mov    ebp,esp
    537:	83 ec 10             	sub    esp,0x10
    53a:	e8 15 00 00 00       	call   554 <__x86.get_pc_thunk.ax>
    53f:	05 9d 1a 00 00       	add    eax,0x1a9d
    544:	8b 55 08             	mov    edx,DWORD PTR [ebp+0x8]	;取第一个参数，即a
    547:	8b 45 0c             	mov    eax,DWORD PTR [ebp+0xc]	;取第二个参数，即b
    54a:	01 d0                	add    eax,edx
    54c:	89 45 fc             	mov    DWORD PTR [ebp-0x4],eax	;[ebp-0x4]即为temp
    54f:	8b 45 fc             	mov    eax,DWORD PTR [ebp-0x4]	;return值传给eax，然后结束
    552:	c9                   	leave  
    553:	c3                   	ret    
   
   ```

2. 通过参数按引用方式返回值：

   给函数传递参数的方式有两种，分别是传值和传引用。进行传值调用时，会建立参数的一份复本，并把它传给调用参数，在调用函数中修改参数值的副本不会影响原始的变量值。传引用调用允许调用函数修改原始变量的值。调用某个函数，当把变量的地址传递给函数时，可以在函数中通过间接引用运算符修改调用函数内存单元中该变量的值。

   ```c++
   #include<iostream>
   using namespace std;
   
   int max1(int &a,int &b)
   {
   	if(a >= b)
   	{
   		return a;
   	}
   	return b;
   }
   
   int max2(int *a,int *b)
   {
   	if(*a >= *b)
   	{
   		return *a;	
   	}
   	return *b;
   }
   
   int main()
   {
   	int a=5,b=6;
   	int result;
   	result = max1(a,b);
   	result = max2(&a,&b);
   
   	return 0;
   }
   
   ```

   ``` assembly
   g++ -o main main.cpp
   objdump -d main -M intel,i386
   00000000000007aa <_Z4max1RiS_>:
    7aa:	55                   	push   ebp
    7ab:	48                   	dec    eax
    7ac:	89 e5                	mov    ebp,esp
    7ae:	48                   	dec    eax
    7af:	89 7d f8             	mov    DWORD PTR [ebp-0x8],edi
    7b2:	48                   	dec    eax
    7b3:	89 75 f0             	mov    DWORD PTR [ebp-0x10],esi
    7b6:	48                   	dec    eax
    7b7:	8b 45 f8             	mov    eax,DWORD PTR [ebp-0x8]
    7ba:	8b 10                	mov    edx,DWORD PTR [eax]
    7bc:	48                   	dec    eax
    7bd:	8b 45 f0             	mov    eax,DWORD PTR [ebp-0x10]
    7c0:	8b 00                	mov    eax,DWORD PTR [eax]
    7c2:	39 c2                	cmp    edx,eax
    7c4:	7c 08                	jl     7ce <_Z4max1RiS_+0x24>
    7c6:	48                   	dec    eax
    7c7:	8b 45 f8             	mov    eax,DWORD PTR [ebp-0x8]
    7ca:	8b 00                	mov    eax,DWORD PTR [eax]
    7cc:	eb 06                	jmp    7d4 <_Z4max1RiS_+0x2a>
    7ce:	48                   	dec    eax
    7cf:	8b 45 f0             	mov    eax,DWORD PTR [ebp-0x10]
    7d2:	8b 00                	mov    eax,DWORD PTR [eax]
    7d4:	5d                   	pop    ebp
    7d5:	c3                   	ret    
   
   00000000000007d6 <_Z4max2PiS_>:
    7d6:	55                   	push   ebp
    7d7:	48                   	dec    eax
    7d8:	89 e5                	mov    ebp,esp
    7da:	48                   	dec    eax
    7db:	89 7d f8             	mov    DWORD PTR [ebp-0x8],edi
    7de:	48                   	dec    eax
    7df:	89 75 f0             	mov    DWORD PTR [ebp-0x10],esi
    7e2:	48                   	dec    eax
    7e3:	8b 45 f8             	mov    eax,DWORD PTR [ebp-0x8]
    7e6:	8b 10                	mov    edx,DWORD PTR [eax]
    7e8:	48                   	dec    eax
    7e9:	8b 45 f0             	mov    eax,DWORD PTR [ebp-0x10]
    7ec:	8b 00                	mov    eax,DWORD PTR [eax]
    7ee:	39 c2                	cmp    edx,eax
    7f0:	7c 08                	jl     7fa <_Z4max2PiS_+0x24>
    7f2:	48                   	dec    eax
    7f3:	8b 45 f8             	mov    eax,DWORD PTR [ebp-0x8]
    7f6:	8b 00                	mov    eax,DWORD PTR [eax]
    7f8:	eb 06                	jmp    800 <_Z4max2PiS_+0x2a>
    7fa:	48                   	dec    eax
    7fb:	8b 45 f0             	mov    eax,DWORD PTR [ebp-0x10]
    7fe:	8b 00                	mov    eax,DWORD PTR [eax]
    800:	5d                   	pop    ebp
    801:	c3                   	ret    
   
   0000000000000802 <main>:
    802:	55                   	push   ebp
    803:	48                   	dec    eax
    804:	89 e5                	mov    ebp,esp
    806:	48                   	dec    eax
    807:	83 ec 20             	sub    esp,0x20
    80a:	64 48                	fs dec eax
    80c:	8b 04 25 28 00 00 00 	mov    eax,DWORD PTR [eiz*1+0x28]
    813:	48                   	dec    eax
    814:	89 45 f8             	mov    DWORD PTR [ebp-0x8],eax
    817:	31 c0                	xor    eax,eax
    819:	c7 45 ec 05 00 00 00 	mov    DWORD PTR [ebp-0x14],0x5
    820:	c7 45 f0 06 00 00 00 	mov    DWORD PTR [ebp-0x10],0x6
    827:	48                   	dec    eax
    828:	8d 55 f0             	lea    edx,[ebp-0x10]
    82b:	48                   	dec    eax
    82c:	8d 45 ec             	lea    eax,[ebp-0x14]
    82f:	48                   	dec    eax
    830:	89 d6                	mov    esi,edx
    832:	48                   	dec    eax
    833:	89 c7                	mov    edi,eax
    835:	e8 70 ff ff ff       	call   7aa <_Z4max1RiS_>
    83a:	89 45 f4             	mov    DWORD PTR [ebp-0xc],eax
    83d:	48                   	dec    eax
    83e:	8d 55 f0             	lea    edx,[ebp-0x10]
    841:	48                   	dec    eax
    842:	8d 45 ec             	lea    eax,[ebp-0x14]
    845:	48                   	dec    eax
    846:	89 d6                	mov    esi,edx
    848:	48                   	dec    eax
    849:	89 c7                	mov    edi,eax
    84b:	e8 86 ff ff ff       	call   7d6 <_Z4max2PiS_>
    850:	89 45 f4             	mov    DWORD PTR [ebp-0xc],eax
    853:	b8 00 00 00 00       	mov    eax,0x0
    858:	48                   	dec    eax
    859:	8b 4d f8             	mov    ecx,DWORD PTR [ebp-0x8]
    85c:	64 48                	fs dec eax
    85e:	33 0c 25 28 00 00 00 	xor    ecx,DWORD PTR [eiz*1+0x28]
    865:	74 05                	je     86c <main+0x6a>
    867:	e8 04 fe ff ff       	call   670 <__stack_chk_fail@plt>
    86c:	c9                   	leave  
    86d:	c3                   	ret    
   
   ```


### 1.3 数据结构

数据结构是计算机存储、组织数据的方式。在进行逆向分析时，确定数据结构以后，算法就很容易得到了。有些时候，事情也会反过来，即依据特定算法来判断数据结构。

1. 局部变量

   局部变量是函数内部定义的一个变量，其作用域和生命周期局限于所在函数内。使用局部变量使程序模块化封装成为了可能。局部变量分配空间时通常会使用栈和寄存器

   - 利用栈存放局部变量

     局部变量在栈中进行分配，函数执行后会释放这些栈空间，程序用`sub esp,8`语句为局部变量分配空间，用`[ebp-xxxx]`寻址调用这些变量，而参数调用相对于`ebp`偏移量是正的，即`[ebp+xxxx]`。在优化模式，编译器通过`esp`寄存器直接对局部变量和参数进行寻址。当函数退出时，用`add esp,8`指令来平衡堆栈。

     局部变量分配与清除栈的形式如下：

     | 形式1     | 形式2      | 形式3    |
     | --------- | ---------- | :------- |
     | sub esp,n | add esp,-n | push reg |
     |……|……|…… |
     |add esp,n|sub esp,-n|pop reg |

     ```c
     int add(int x,int y)
     {
         int z;
        	z=x+y;
        	return z;
     }
     int main()
     {
         int a=5,b=6;
         add(a,b);
         return 0;
     }
     ```

     ```assembly
     ;add(int x,int y)
     00401080   push        ebp
     00401081   mov         ebp,esp
     00401083   sub         esp,44h
     00401086   push        ebx
     00401087   push        esi
     00401088   push        edi
     00401089   lea         edi,[ebp-44h]
     0040108C   mov         ecx,11h
     00401091   mov         eax,0CCCCCCCCh
     00401096   rep stos    dword ptr [edi]
     00401098   mov         eax,dword ptr [ebp+8]	;
     0040109B   add         eax,dword ptr [ebp+0Ch]
     0040109E   mov         dword ptr [ebp-4],eax
     004010A1   mov         eax,dword ptr [ebp-4]
     004010A4   pop         edi
     004010A5   pop         esi
     004010A6   pop         ebx
     004010A7   mov         esp,ebp
     004010A9   pop         ebp
     004010AA   ret
     
     ;main()
     00401020   push        ebp
     00401021   mov         ebp,esp
     00401023   sub         esp,48h
     00401026   push        ebx
     00401027   push        esi
     00401028   push        edi
     00401029   lea         edi,[ebp-48h]
     0040102C   mov         ecx,12h
     00401031   mov         eax,0CCCCCCCCh
     00401036   rep stos    dword ptr [edi]
     00401038   mov         dword ptr [ebp-4],5
     0040103F   mov         dword ptr [ebp-8],6
     00401046   push        6
     00401048   push        5
     0040104A   call        @ILT+0(add) (00401005)
     0040104F   add         esp,8
     00401052   xor         eax,eax
     00401054   pop         edi
     00401055   pop         esi
     00401056   pop         ebx
     00401057   add         esp,48h
     0040105A   cmp         ebp,esp
     0040105C   call        __chkesp (004010c0)
     00401061   mov         esp,ebp
     00401063   pop         ebp
     00401064   ret
     
     ```

     由于局部变量的初始值是其他函数执行后留在栈中的垃圾数据，因此需要对其进行初始化。初始化局部变量有两种方法：一是通过`mov`指令进行变量赋值，例如`mov dword ptr [ebp-4],5`；另一种是使用`push`指令直接将值压入栈中，例如`push 5`。

   

   - 利用寄存器存放局部变量

     除了栈占用两个寄存器，编译器会利用剩下的6个通用寄存器尽可能有效地存放局部变量，这样可以少产生代码，提高程序的效率。如果寄存器不够用，编译就会将变量放到栈中。在进行逆向分析时要注意，局部变量的生存周期比较短，必须及时确定当前寄存器的变量是哪个变量。

2. 全局变量

   全局变量作用于整个程序，存放在静态存储区中。局部变量存在于栈区，函数调用结束后便会消失。大多数程序的常数常放在全局变量中。

   在`PE`结构中，全局变量通常位于数据区块`.data`的一个固定地址处，当程序需要访问全局变量时，一般会用一个固定的硬编码地址直接对内存进行寻址。若放在`.rdata`中，则是一个常量。

   全局变量可以被同一文件的所有函数修改，因而的可以利用其来传递参数和函数返回值。全局变量在程序的整个执行过程中占用内存单元，而不像局部变量那样在需要时才开辟内存单元。

   ```c
   #include<stdio.h>
   int gl;
   int add(int x,int y)
   {
   	int z;
   	z=x+y;
   	return z;
   }
   
   int main()
   {
   	int a=5,b=6;
   	add(a,b);
   	gl=123456;
   	return 0;
   }
   ```

   ```assembly
   0040D4E0   push        ebp
   0040D4E1   mov         ebp,esp
   0040D4E3   sub         esp,48h
   0040D4E6   push        ebx
   0040D4E7   push        esi
   0040D4E8   push        edi
   0040D4E9   lea         edi,[ebp-48h]
   0040D4EC   mov         ecx,12h
   0040D4F1   mov         eax,0CCCCCCCCh
   0040D4F6   rep stos    dword ptr [edi]
   0040D4F8   mov         dword ptr [ebp-4],5
   0040D4FF   mov         dword ptr [ebp-8],6
   0040D506   mov         eax,dword ptr [ebp-8]
   0040D509   push        eax
   0040D50A   mov         ecx,dword ptr [ebp-4]
   0040D50D   push        ecx
   0040D50E   call        @ILT+10(_add) (0040100f)
   0040D513   add         esp,8
   0040D516   mov         dword ptr [_gl (00427e3c)],1E240h
   0040D520   xor         eax,eax
   0040D522   pop         edi
   0040D523   pop         esi
   0040D524   pop         ebx
   0040D525   add         esp,48h
   0040D528   cmp         ebp,esp
   0040D52A   call        __chkesp (004010c0)
   0040D52F   mov         esp,ebp
   0040D531   pop         ebp
   0040D532   ret
   
   ```

   与全局变量类似的是静态变量，它们都可以按直接方式寻址等。不同的是，静态变量的作用范围有限，仅在定义这些变量的函数内有效。

3. 数组

   数组是相同类型的元素的集合，它们在内存中按顺序连续存放在一起。访问数组一般是通过基址加变址寻址来实现的。

   ```c
   #include<stdio.h>
   
   int main(void)
   {
   	static int a[3]={0x11,0x22,0x33};
   	int i,s=0,b[3];
   	for(i=0;i<3;++i)
   	{
   		s+=a[i];
   		b[i]=s;
   	}
   	for(i=0;i<3;++i)
   	{
   		printf("%d\n",b[i]);
   	}
   	return 0;
   }
   ```

   ```assembly
   0040D7F0   push        ebp
   0040D7F1   mov         ebp,esp
   0040D7F3   sub         esp,54h
   0040D7F6   push        ebx
   0040D7F7   push        esi
   0040D7F8   push        edi
   0040D7F9   lea         edi,[ebp-54h]
   0040D7FC   mov         ecx,15h
   0040D801   mov         eax,0CCCCCCCCh
   0040D806   rep stos    dword ptr [edi]
   0040D808   mov         dword ptr [ebp-8],0	;s=0
   0040D80F   mov         dword ptr [ebp-4],0	;i=0
   0040D816   jmp         main+31h (0040d821)
   0040D818   mov         eax,dword ptr [ebp-4]	
   0040D81B   add         eax,1
   0040D81E   mov         dword ptr [ebp-4],eax	;++i
   0040D821   cmp         dword ptr [ebp-4],3
   0040D825   jge         main+53h (0040d843)
   0040D827   mov         ecx,dword ptr [ebp-4]
   0040D82A   mov         edx,dword ptr [ebp-8]
   0040D82D   add         edx,dword ptr [ecx*4+427320h]	;s+=a[i]
   0040D834   mov         dword ptr [ebp-8],edx	
   0040D837   mov         eax,dword ptr [ebp-4]
   0040D83A   mov         ecx,dword ptr [ebp-8]
   0040D83D   mov         dword ptr [ebp+eax*4-14h],ecx	;b[i]=s;
   0040D841   jmp         main+28h (0040d818)
   0040D843   mov         dword ptr [ebp-4],0
   0040D84A   jmp         main+65h (0040d855)
   0040D84C   mov         edx,dword ptr [ebp-4]
   0040D84F   add         edx,1
   0040D852   mov         dword ptr [ebp-4],edx
   0040D855   cmp         dword ptr [ebp-4],3
   0040D859   jge         main+82h (0040d872)
   0040D85B   mov         eax,dword ptr [ebp-4]
   0040D85E   mov         ecx,dword ptr [ebp+eax*4-14h]
   0040D862   push        ecx
   0040D863   push        offset string "%d\n" (0042210c)
   0040D868   call        printf (0040d770)
   0040D86D   add         esp,8
   0040D870   jmp         main+5Ch (0040d84c)
   0040D872   xor         eax,eax
   0040D874   pop         edi
   0040D875   pop         esi
   0040D876   pop         ebx
   0040D877   add         esp,54h
   0040D87A   cmp         ebp,esp
   0040D87C   call        __chkesp (004010c0)
   0040D881   mov         esp,ebp
   0040D883   pop         ebp
   0040D884   ret
   ```

   在内存中，数组可存在于栈、数据段及动态内存中，其寻址用`基址+偏移量`实现

   ```assembly
   mov eax, dword ptr[427320h+ecx*4]	;427320h为基址，4*ecx为偏移量
   ```

   间接寻址一般出现在给一些数组或结构体赋值的情况下，其寻址形式一般是[基址+n]。



### 1.4虚函数

虚函数是在程序运行时定义的函数。虚函数的地址不能编译时确定，只能在调用即将进行时确定。所有对虚函数的引用通常都放在一个专用数组——虚函数表`VTBL`中，数组元素就是类中虚函数`VPTR`的地址。

调用虚函数时，程序先取出虚函数表指针，得到虚函数表的地址，然后根据这个地址从虚函数表中取出该函数的地址，最后调用该函数。

```c++
#include<iostream>
using namespace std;

class CSum
{
public:
	virtual int Add(int a,int b)
	{
		return a+b;
	}

	virtual int Sub(int a,int b)
	{
		return a-b;
	}

};

void main()
{
	CSum *pCSum = new CSum();
	pCSum->Add(1,2);
	pCSum->Sub(1,2);

}
```



```assembly
0040107D   push        4
0040107F   call        operator new (004082d0)	;执行new，为CSum分配4个字节的空间
00401084   add         esp,4
00401087   mov         dword ptr [ebp-18h],eax
0040108A   mov         dword ptr [ebp-4],0
00401091   cmp         dword ptr [ebp-18h],0
00401095   je          main+54h (004010a4)
00401097   mov         ecx,dword ptr [ebp-18h]
0040109A   call        @ILT+25(CSum::CSum) (0040101e)	;CSum的默认构造CSum()
0040109F   mov         dword ptr [ebp-1Ch],eax
004010A2   jmp         main+5Bh (004010ab)
004010A4   mov         dword ptr [ebp-1Ch],0
004010AB   mov         eax,dword ptr [ebp-1Ch]
004010AE   mov         dword ptr [ebp-14h],eax
004010B1   mov         dword ptr [ebp-4],0FFFFFFFFh
004010B8   mov         ecx,dword ptr [ebp-14h]
004010BB   mov         dword ptr [ebp-10h],ecx
004010BE   mov         esi,esp
004010C0   push        2
004010C2   push        1
004010C4   mov         edx,dword ptr [ebp-10h]	;数据栈中ebp-10h的内存的值即为CSum的实例对象指针pCSum
											;edx=*pCSum，即edx=&VTBL
004010C7   mov         eax,dword ptr [edx]		;eax=VTBL
004010C9   mov         ecx,dword ptr [ebp-10h]	;ecx=&VTBL
004010CC   call        dword ptr [eax]			;执行Add(),call VTBL[0],( 即*(VTBL+0) )
004010CE   cmp         esi,esp
004010D0   call        __chkesp (004087c0)
004010D5   mov         esi,esp
004010D7   push        2
004010D9   push        1
004010DB   mov         ecx,dword ptr [ebp-10h]	;数据栈中ebp-10h的内存的值即为CSum的实例对象指针pCSum
											;ecx=*pCSum，即ecx=&VTBL
004010DE   mov         edx,dword ptr [ecx]		;edx=VTBL
004010E0   mov         ecx,dword ptr [ebp-10h]	;ecx=&VTBL
004010E3   call        dword ptr [edx+4]		;执行Sub(),call VTBL[1],( 即*(VTBL+4) )
004010E6   cmp         esi,esp
004010E8   call        __chkesp (004087c0)
004010ED   mov         ecx,dword ptr [ebp-0Ch]
004010F0   mov         dword ptr fs:[0],ecx
004010F7   pop         edi
004010F8   pop         esi
004010F9   pop         ebx
004010FA   add         esp,5Ch
004010FD   cmp         ebp,esp
004010FF   call        __chkesp (004087c0)
00401104   mov         esp,ebp
00401106   pop         ebp
00401107   ret
```

虚函数是通过指向虚函数表的指针间接地加以调用的，期间经历了两次间接寻址。

### 1.5 控制语句

1. `if-else`语句：

   `if-else`语句编译成汇编代码后，整数用`cmp`指令进行比较，浮点数值用`fcom`、`fcomp`等指令进行比较。

   `cmp`指令不会修改操作数，它通过两个操作数相减的结果来影响诸如零标志、进位标志、符号标志和溢出标志等的标志位。实际上，在很多情况下编译器都使用`test`或`or`之类较短的逻辑指令来替换`cmp`指令，形式通常为`test eax eax`。当`eax`的值为0时，则其逻辑与运算结果为0，设置`ZF`为1，否则设置为0。

   ```c++
   void func()
   {
   	int a = 0;
   	if(a == 0)
   	{
   		printf("Enter in if-segement!\n");
   	}
   	else
   	{
   		printf("Enter in else-segement!\n");
   	}
   	return;
   }
   ```

   ```assembly
   .text:00401048                 mov     [ebp+var_4], 0
   .text:0040104F                 cmp     [ebp+var_4], 0 ;var_4即为变量a在栈中的相对ebp的偏移量
   .text:00401053                 jnz     short loc_401064 ;一般而言，跳转指令的条件恰好与源代码的判断													 ;条件相反
   .text:00401055                 push    offset aEnterInIfSegem ; "Enter in if-segement!\n"
   .text:0040105A                 call    _printf
   .text:0040105F                 add     esp, 4
   .text:00401062                 jmp     short loc_401071
   .text:00401064                 push    offset aEnterInElseSeg ; "Enter in else-segement!\n"
   .text:00401069                 call    _printf
   ```

   

2. `switch-case`语句：

   `switch-case`语句是多分支选择语句。编译后的`SWITCH`语句，其实质即为多个跳转语句的嵌套组合。编译器会将`SWITCH`语句编译成一组由不同的关系运算组成的语句。

   ```c++
   void func()
   {
   	char ch = '0';
   	switch(ch)
   	{
   	case '2':
   		printf("ch = '2'\n");
   		break;
   	case '1':
   		printf("ch = '1'\n");
   		break;
   	case '0':
   		printf("ch = '0'\n");
   		break;
   	default:
   		printf("Enter default-segement!\n");
   	}
   	return;
   }
   ```

   ```assembly
   00401048   mov         byte ptr [ebp-4],30h 
   0040104C   mov         al,byte ptr [ebp-4]
   0040104F   mov         byte ptr [ebp-8],al ;ch = '0'
   00401052   cmp         byte ptr [ebp-8],30h
   00401056   je          func+54h (00401084) ;若ch - '0' == 0，则跳转
   00401058   cmp         byte ptr [ebp-8],31h
   0040105C   je          func+45h (00401075) ;若ch - '1' == 0，则跳转
   0040105E   cmp         byte ptr [ebp-8],32h
   00401062   je          func+36h (00401066) ;若ch - '2' == 0，则跳转
   00401064   jmp         func+63h (00401093) ;若都不是则跳转至default
   00401066   push        offset string "ch = '2'\n" (00431054)
   0040106B   call        printf (004081f0)
   00401070   add         esp,4
   00401073   jmp         func+70h (004010a0)
   00401075   push        offset string "ch = '1'\n" (00431048)
   0040107A   call        printf (004081f0)
   0040107F   add         esp,4
   00401082   jmp         func+70h (004010a0)
   00401084   push        offset string "ch = '0'\n" (0043103c)
   00401089   call        printf (004081f0)
   0040108E   add         esp,4
   00401091   jmp         func+70h (004010a0)
   00401093   push        offset string "Enter default-segement!\n" (0043101c)
   00401098   call        printf (004081f0)
   
   ```

   在实际中，`case`下可能还包含了其他的跳转分支语句、循环语句等，这会使得问题变得更为复杂。

### 1.6 循环语句

循环是高级语言中可以反向引用得一种语言形式。其他类型诸如`if-else`等得分支语句都是由低地址到高地址区域引用的。通过这一点可以方便地将循环语句识别出来。

如果确定某段代码是循环代码，就可以分析其计数器，通常是用`ecx`作为计数器。

```c++
void func1()
{
	int i;
	for(i = 0;i < 10;++i)
	{
		printf("i = %d\n",i);
	}
}
void func2()
{
	int i;
	i = 0;
	while(i < 10)
	{
		printf("i = %d\n",i);
		i++;
	}
}
void func3()
{
	int i;
	i = 0;
	do
	{
		printf("i = %d\n",i);
		i++;
	}while(i < 10);
}
```

```assembly
func1_loop:
00401068   mov         dword ptr [ebp-4],0 ;i=0
0040106F   jmp         func1+2Ah (0040107a)
00401071   mov         eax,dword ptr [ebp-4]
00401074   add         eax,1
00401077   mov         dword ptr [ebp-4],eax ;i++
0040107A   cmp         dword ptr [ebp-4],0Ah
0040107E   jge         func1+43h (00401093) ;若i-10 >= 0，则跳转，即跳出该循环
00401080   mov         ecx,dword ptr [ebp-4]
00401083   push        ecx
00401084   push        offset string "i = %d\n" (0043101c)
00401089   call        printf (004082c0)
0040108E   add         esp,8
00401091   jmp         func1+21h (00401071) ;循环内部语句执行完毕后，跳转回低地址

func2_loop:
004010D8   mov         dword ptr [ebp-4],0 ;i=0
004010DF   cmp         dword ptr [ebp-4],0Ah
004010E3   jge         func2+41h (00401101) ;如果i-10 >= 0，则跳出循环，否则就进入循环体内
004010E5   mov         eax,dword ptr [ebp-4]
004010E8   push        eax
004010E9   push        offset string "i = %d\n" (0043101c)
004010EE   call        printf (004082c0)
004010F3   add         esp,8
004010F6   mov         ecx,dword ptr [ebp-4]
004010F9   add         ecx,1
004010FC   mov         dword ptr [ebp-4],ecx ;i++
004010FF   jmp         func2+1Fh (004010df)

func3_loop:
00401148   mov         dword ptr [ebp-4],0 ;i=0
0040114F   mov         eax,dword ptr [ebp-4]
00401152   push        eax
00401153   push        offset string "i = %d\n" (0043101c)
00401158   call        printf (004082c0)
0040115D   add         esp,8
00401160   mov         ecx,dword ptr [ebp-4]
00401163   add         ecx,1
00401166   mov         dword ptr [ebp-4],ecx ;i++
00401169   cmp         dword ptr [ebp-4],0Ah
0040116D   jl          func3+1Fh (0040114f) ;若i-10 < 0，则跳转回低地址继续循环，否则循环结束，执行下一										  ;行代码

```

### 1.7 数学运算符

1. 整数加减法：

   一般情况下，整数的加减法分别会被编译成`add`和`sub`指令。在编译优化时，很多人喜欢用`lea`指令来代替`add`和`sub`指令。`lea`指令允许用户在1个时钟内完成对`c=a+b+78h`的计算，其中`a`、`b`、`c`都是在有寄存器的情况下才有效。

   ```c++
   void func1()
   {
   	int a,b;
   	printf("%d\n",a+b+0x78);
   }
   ```

   ```assembly
   func1:
   00401048   mov         eax,dword ptr [ebp-8]
   0040104B   mov         ecx,dword ptr [ebp-4]
   0040104E   lea         edx,[ecx+eax+78h] ;lea c,[a+b+0x78]
   00401052   push        edx
   00401053   push        offset string "%d\n" (0043101c)
   00401058   call        printf (004081a0)
   ```

   

2. 整数乘法：

   乘法运算符一般被编译成`mul`、`imul`等指令，其运行速度较慢。编译器为了提高运算的效率，倾向于使用其他指令来完成同样的运算。如果一个数是2的幂，那么会用左移命令`shl`来实现乘法运算。另外，加法对于提高3、5、6、7、9等数的乘法运算效率非常有用。

   ```c++
   void func1()
   {
   	int a;
   	printf("%d %d %d\n",a*11+4,a*9,a*2);
   }
   ```

   ```assembly
   func1:
   00401048   mov         eax,dword ptr [ebp-4]
   0040104B   shl         eax,1
   0040104D   push        eax ;a*2入栈
   0040104E   mov         ecx,dword ptr [ebp-4]
   00401051   imul        ecx,ecx,9
   00401054   push        ecx ;a*9入栈
   00401055   mov         edx,dword ptr [ebp-4]
   00401058   imul        edx,edx,0Bh
   0040105B   add         edx,4
   0040105E   push        edx ;a*11+4入栈
   0040105F   push        offset string "%d %d %d\n" (0043101c)
   00401064   call        printf (004081a0)
   ```

   

3. 整数除法：

### 1.8 文本字符串

### 1.9 指令修改技巧















































